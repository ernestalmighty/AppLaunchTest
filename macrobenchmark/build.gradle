plugins {
    id 'com.android.test'
    id 'kotlin-android'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 29
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["androidx.benchmark.suppressErrors"] = "EMULATOR,DEBUGGABLE"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        // Declare a build type (release) to match the target app's build type
        release {
            signingConfig = debug.signingConfig
        }
    }

    targetProjectPath = ":main" // Note that your module name may be different

    // Enable the benchmark to run separately from the app process
    experimentalProperties["android.experimental.self-instrumenting"] = true

}

androidComponents {
    beforeVariants(selector().all()) {
        // Enable only the benchmark buildType, since we only want to measure
        // release-like build performance (should match app buildType)
        enabled = buildType == 'release'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'junit:junit:4.+'
    implementation 'androidx.test.ext:junit:1.1.3'
    implementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.1.0-alpha08'
}